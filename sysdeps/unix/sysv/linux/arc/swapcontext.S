/* Save and set current context for ARC.
   Copyright (C) 2020-2023 Free Software Foundation, Inc.
   This file is part of the GNU C Library.

   The GNU C Library is free software; you can redistribute it and/or
   modify it under the terms of the GNU Lesser General Public
   License as published by the Free Software Foundation; either
   version 2.1 of the License, or (at your option) any later version.

   The GNU C Library is distributed in the hope that it will be useful,
   but WITHOUT ANY WARRANTY; without even the implied warranty of
   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
   Lesser General Public License for more details.

   You should have received a copy of the GNU Lesser General Public
   License along with the GNU C Library.  If not, see
   <https://www.gnu.org/licenses/>.  */

#include "ucontext-macros.h"

/* int swapcontext (ucontext_t *oucp, const ucontext_t *ucp).  */

ENTRY (__swapcontext)

	/* Save context into @oucp pointed to by r0.  */

	ADDR   r2, r0, UCONTEXT_MCONTEXT
	STR.as r13,   r2, 37
	STR.as r14,   r2, 36
	STR.as r15,   r2, 35
	STR.as r16,   r2, 34
	STR.as r17,   r2, 33
	STR.as r18,   r2, 32
	STR.as r19,   r2, 31
	STR.as r20,   r2, 30
	STR.as r21,   r2, 29
	STR.as r22,   r2, 28
	STR.as r23,   r2, 27
	STR.as r24,   r2, 26

	STR.as blink, r2,  7
	STR.as fp,    r2,  8
	STR.as gp,    r2,  9
	STR.as sp,    r2, 23

#if defined (__ARC_FLOAT_ABI_HARD__)
	FSTR.as	f16,  r2, 51
	FSTR.as	f17,  r2, 52
	FSTR.as	f18,  r2, 53
	FSTR.as	f19,  r2, 54
	FSTR.as	f20,  r2, 55
	FSTR.as	f21,  r2, 56
	FSTR.as	f22,  r2, 57
	FSTR.as	f23,  r2, 58
	FSTR.as	f24,  r2, 59
	FSTR.as	f25,  r2, 60
	FSTR.as	f26,  r2, 61
	FSTR.as	f27,  r2, 62
	FSTR.as	f28,  r2, 63
	FSTR.as	f29,  r2, 64
	FSTR.as	f30,  r2, 65
	FSTR.as	f31,  r2, 66
#endif

	/* Save 0 in r0 placeholder to return 0 when @oucp activated.  */
	mov   r9, 0
	STR.as r9,    r2, 22

	/* Load context from @ucp.  */

	MOVR   r9, r1	/* Safekeep @ucp across syscall.  */

	/* rt_sigprocmask (SIG_SETMASK, &ucp->uc_sigmask, &oucp->uc_sigmask, _NSIG8) */
	mov   r3, _NSIG8
	ADDR   r2, r0, UCONTEXT_SIGMASK
	ADDR   r1, r1, UCONTEXT_SIGMASK
	mov   r0, SIG_SETMASK
	mov   r8, __NR_rt_sigprocmask
	ARC_TRAP_INSN
	BRRhi  r0, -1024, L (call_syscall_err)

	ADDR   r9, r9, UCONTEXT_MCONTEXT
	LDR.as r0,    r9, 22
	LDR.as r1,    r9, 21
	LDR.as r2,    r9, 20
	LDR.as r3,    r9, 19
	LDR.as r4,    r9, 18
	LDR.as r5,    r9, 17
	LDR.as r6,    r9, 16
	LDR.as r7,    r9, 15

	LDR.as r13,   r9, 37
	LDR.as r14,   r9, 36
	LDR.as r15,   r9, 35
	LDR.as r16,   r9, 34
	LDR.as r17,   r9, 33
	LDR.as r18,   r9, 32
	LDR.as r19,   r9, 31
	LDR.as r20,   r9, 30
	LDR.as r21,   r9, 29
	LDR.as r22,   r9, 28
	LDR.as r23,   r9, 27
	LDR.as r24,   r9, 26

	LDR.as blink, r9,  7
	LDR.as fp,    r9,  8
	LDR.as gp,    r9,  9
	LDR.as sp,    r9, 23

	j    [blink]

PSEUDO_END (__swapcontext)
weak_alias (__swapcontext, swapcontext)
